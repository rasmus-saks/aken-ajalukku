apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.github.rasmussaks.akenajalukku"
        minSdkVersion 16
        targetSdkVersion 24
        def greenhouseBuild = System.getenv()["GREENHOUSE_BUILD"]
        logger.quiet(System.getenv().toMapString())
        if (greenhouseBuild) {
            logger.quiet("GREENHOUSE_BUILD variable is true")
            def buildNumber = System.getenv()["BUILD_NUMBER"]
            logger.quiet("BUILD_NUMBER: " + buildNumber)
            versionCode = Integer.valueOf(buildNumber)
        } else {
            logger.quiet("GREENHOUSE_BUILD variable is false")
            versionCode 2
        }
        versionName "iteration-2"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "com.github.rasmussaks.akenajalukku.CustomTestRunner"
    }
    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Greenhouse
                storeFile file(System.getenv()["GH_KEYSTORE_PATH"])
                storePassword System.getenv()["GH_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["GH_KEY_ALIAS"]
                keyPassword System.getenv()["GH_KEY_PASSWORD"]
            } else if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            if (project.hasProperty("RELEASE_STORE_FILE") || System.getenv()["CI"]) {
                signingConfig signingConfigs.release
            }
            resValue "string", "google_maps_key", 'AIzaSyB7hLpT4NB60q76zwqqE1USAve0fxHfJ-g'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            if (System.getenv()["CI"]) {
                resValue "string", "google_maps_key", System.getenv()["MAPS_API_KEY"]
                logger.quiet("MAPS KEY IS: " + System.getenv()["MAPS_API_KEY"])
            } else {
                resValue "string", "google_maps_key", 'AIzaSyB7hLpT4NB60q76zwqqE1USAve0fxHfJ-g'
                logger.quiet("MAPS KEY IS: default")
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.google.android.gms:play-services-maps:9.6.1'
    compile 'com.google.android.gms:play-services-location:9.6.1'
    compile 'com.akexorcist:googledirectionlibrary:1.0.4'
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:support-vector-drawable:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.sothree.slidinguppanel:library:3.3.1'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:24.2.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}
