apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.github.rasmussaks.akenajalukku"
        minSdkVersion 16
        targetSdkVersion 24
        def greenhouseBuild = System.getenv()["GREENHOUSE_BUILD"]
        def travisBuild = System.getenv()["TRAVIS"]
        logger.quiet(System.getenv().toMapString())
        if (greenhouseBuild) {
            logger.quiet("GREENHOUSE_BUILD variable is true")
            def buildNumber = System.getenv()["BUILD_NUMBER"]
            logger.quiet("BUILD_NUMBER: " + buildNumber)
            versionCode = Integer.valueOf(buildNumber)
        } else if (travisBuild) {
            logger.quiet("TRAVIS variable is true")
            def buildNumber = Integer.valueOf(System.getenv()["TRAVIS_BUILD_NUMBER"]) + 100
            logger.quiet("BUILD_NUMBER: " + buildNumber)
            versionCode = buildNumber
        } else {
            logger.quiet("GREENHOUSE_BUILD variable is false")
            versionCode 2
        }
        versionName "iteration-3"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "com.github.rasmussaks.akenajalukku.CustomTestRunner"
    }
    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Greenhouse
                storeFile file(System.getenv()["GH_KEYSTORE_PATH"])
                storePassword System.getenv()["GH_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["GH_KEY_ALIAS"]
                keyPassword System.getenv()["GH_KEY_PASSWORD"]
            } else if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        def mapsKey = "AIzaSyB7hLpT4NB60q76zwqqE1USAve0fxHfJ-g"
        if (System.getenv()["MAPS_API_KEY"]) mapsKey = System.getenv()["MAPS_API_KEY"]
        else if (project.hasProperty("GOOGLE_MAPS_KEY")) mapsKey = GOOGLE_MAPS_KEY
        release {
            minifyEnabled false
            if (project.hasProperty("RELEASE_STORE_FILE") || System.getenv()["CI"]) {
                signingConfig signingConfigs.release
            }
            resValue "string", "google_maps_key", mapsKey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "google_maps_key", mapsKey
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.google.android.gms:play-services-maps:9.6.0'
    compile 'com.google.android.gms:play-services-location:9.6.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:support-vector-drawable:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.sothree.slidinguppanel:library:3.3.1'
    compile 'com.afollestad:easyvideoplayer:0.2.13'
    compile 'com.google.maps.android:android-maps-utils:0.4.4'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}
